apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

def isCi = 'true'.equals(System.getenv('CI'))
def preDexEnabled = 'true'.equals(System.getProperty('pre-dex', 'true'))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    signingConfigs {
        nabijam {
            storeFile file('nabijam.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    defaultConfig {
        applicationId "com.twentyhours.nabijam"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "0.1.0"
        signingConfig signingConfigs.nabijam
        multiDexEnabled = true
    }

    dexOptions {
        maxProcessCount 4 // this is the default value
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        production {
            applicationId "com.twentyhours.nabijam"
        }
        staging {
            applicationId "com.twentyhours.nabijam.dev"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix '-DEBUG'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
//        fatal 'UnusedResources'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        sourceSets {
            test.java.srcDirs += 'src/test'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:multidex:1.0.1'
    kapt 'com.android.databinding:compiler:2.3.0-beta4' // Android Data Binding

    compile 'com.madgag.spongycastle:core:1.54.0.0' // Crypto
    compile 'com.madgag.spongycastle:prov:1.54.0.0'
    compile 'com.madgag.spongycastle:pkix:1.54.0.0'
    compile 'com.madgag.spongycastle:pg:1.54.0.0'

    kapt "com.google.dagger:dagger-compiler:2.6.1" // Dependency Injection
    compile "com.google.dagger:dagger:2.6.1"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'io.reactivex:rxkotlin:0.60.0' // Rx for Kotlin
    compile 'io.netty:netty-all:4.0.24.Final'

    compile 'com.jakewharton.timber:timber:4.4.0'

    testCompile "org.mockito:mockito-core:2.+"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}

kapt {
    generateStubs = true
}
